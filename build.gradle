plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.1'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.15.0'
}

group = 'com.wolper'
version = '1'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.security:spring-security-messaging'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.webflow:spring-webflow:3.0.0'
    implementation 'io.projectreactor.netty:reactor-netty:1.1.17'
    implementation'mysql:mysql-connector-java:8.0.33'
    runtimeOnly 'com.microsoft.sqlserver:mssql-jdbc'

    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

task copyJacocoReport(type: Copy) {
    from layout.buildDirectory.dir("jacocoCsv")
    into layout.projectDirectory.dir("src/main/java/wolper/coverage/jacoco")
    include '*.*'
}

test {
    useJUnitPlatform()
    maxHeapSize = '1G'
    testLogging {
        events "passed"
    }
    finalizedBy jacocoTestReport
}

pitest {
    targetClasses = ['wolper.**.*']
    excludedClasses = ['wolper.application.**.*', 'wolper.messaging.**.*', 'persistence.inmemory.**.*']
    junit5PluginVersion = '1.2.1'
    pitestVersion = '1.15.2'
    threads = 10
    outputFormats = ['HTML']
}


jacocoTestReport {
    dependsOn test
    reports {
            xml.required = false
            csv.required = true
            csv.outputLocation = layout.buildDirectory.file('jacocoCsv/jacoco.csv')
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['wolper/application/*',
                              'wolper/messaging/*',
                              'wolper/web/home/*',
                              'wolper/domain/*'])
        })
    }
    finalizedBy copyJacocoReport
}






